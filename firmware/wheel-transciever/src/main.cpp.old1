#include <Arduino.h>
#include <BLE2902.h>
#include <BLEClient.h>
#include <BLEDevice.h>
#include <BLEServer.h>
#include <BLEUtils.h>

#include "button_handler.hpp"

#define SERVICE_UUID "4fafc201-1fb5-459e-8fcc-c5c9c331914b"
#define CHARACTERISTIC_UUID "beb5483e-36e1-4688-b7f5-ea07361b26a8"
#define ADVERTISING_INTERVAL 500
#define INTERIOR_LIGHT_ID 22  // ID for interior light state
#define BACKLIGHT_PIN D7      // Pin for the backlight

BLEServer* pServer = NULL;
BLECharacteristic* pCharacteristic = NULL;
bool deviceConnected = false;
bool oldDeviceConnected = false;
unsigned long lastAdvertisingTime = 0;
unsigned long lastBacklightToggleTime = 0;
bool backlightState = false;

class MyServerCallbacks : public BLEServerCallbacks {
  void onConnect(BLEServer* pServer) {
    deviceConnected = true;
    Serial.println("Connected");
  };

  void onDisconnect(BLEServer* pServer) {
    deviceConnected = false;
    Serial.println("Disconnected");
  }
};

void startAdvertising() {
  BLEAdvertising* pAdvertising = BLEDevice::getAdvertising();
  BLEAdvertisementData advertisementData;
  advertisementData.setCompleteServices(BLEUUID(SERVICE_UUID));
  advertisementData.setName("XIAO_ESP32S3_WHEEL");
  advertisementData.setManufacturerData(
      std::string(deviceConnected ? "C" : "D").c_str());

  BLEAdvertisementData scanResponseData;
  scanResponseData.setCompleteServices(BLEUUID(SERVICE_UUID));
  pAdvertising->setScanResponseData(scanResponseData);
  pAdvertising->setAdvertisementData(advertisementData);

  BLEDevice::startAdvertising();
  lastAdvertisingTime = millis();
}

void setup() {
  Serial.begin(115200);
  Serial.println("Starting BLE...");

  BLEDevice::init("XIAO_ESP32C3");
  pServer = BLEDevice::createServer();
  pServer->setCallbacks(new MyServerCallbacks());

  BLEService* pService = pServer->createService(SERVICE_UUID);
  pCharacteristic = pService->createCharacteristic(
      CHARACTERISTIC_UUID, BLECharacteristic::PROPERTY_READ |
                               BLECharacteristic::PROPERTY_WRITE |
                               BLECharacteristic::PROPERTY_NOTIFY);

  pCharacteristic->addDescriptor(new BLE2902());
  pService->start();

  BLEAdvertising* pAdvertising = BLEDevice::getAdvertising();
  pAdvertising->setScanResponse(true);
  pAdvertising->setMinPreferred(0x06);
  pAdvertising->setMaxPreferred(0x12);
  pAdvertising->setMinInterval(0x20);
  pAdvertising->setMaxInterval(0x40);

  buttonHandlerSetup();

  pinMode(BACKLIGHT_PIN, OUTPUT);    // Initialize backlight pin as output
  digitalWrite(BACKLIGHT_PIN, LOW);  // Initially off

  startAdvertising();
  Serial.println("Ready");
}

void loop() {
  if (!deviceConnected && oldDeviceConnected) {
    delay(500);
    startAdvertising();
    oldDeviceConnected = deviceConnected;
  }

  if (deviceConnected && !oldDeviceConnected) {
    oldDeviceConnected = deviceConnected;
  }

  if (!deviceConnected &&
      (millis() - lastAdvertisingTime >= ADVERTISING_INTERVAL)) {
    startAdvertising();
  }

  uint8_t buttonState = getButtonState();
  if (buttonState != 0 && deviceConnected) {
    pCharacteristic->setValue(&buttonState, 1);
    pCharacteristic->notify();
  }

  if (deviceConnected) {
    std::string value = pCharacteristic->getValue();
    if (value.length() > 0) {
      uint8_t receivedState = value[0];
      if ((receivedState & 0x7F) == INTERIOR_LIGHT_ID) {
        digitalWrite(BACKLIGHT_PIN, !(receivedState & 0x80));
      }
    }
  } else {  // Disconnected, toggle backlight
    if (millis() - lastBacklightToggleTime >= 500) {
      Serial.print("Toggled");
      Serial.println(lastBacklightToggleTime);
      backlightState = !backlightState;
      digitalWrite(BACKLIGHT_PIN, backlightState);
      lastBacklightToggleTime = millis();
    }
  }

  delay(10);
}